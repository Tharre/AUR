# Maintainer: Tharre <tharre3@gmail.com>

pkgname=st-custom
_pkgname=st
pkgver=0.6.r19.gee5cad4
pkgrel=1
pkgdesc='Simple virtual terminal emulator for X'
url='http://git.suckless.org/st/'
arch=('i686' 'x86_64' 'armv7h')
license=('MIT')
depends=('libxft')
makedepends=('ncurses' 'libxext' 'git')
source=('git://git.suckless.org/st' 'config.h' 'st-terminal.desktop'
        '0001-Add-transparency.patch')
md5sums=('SKIP'
         'd5c73780a8cfc8df4133653dfd99ecc2'
         'bbe3714218145d39b6920961f27692e6'
         '2fb7d75cac7ebf49dbdd43eeed955020')

provides=("${_pkgname}")
conflicts=("${_pkgname}")

pkgver() {
	cd "${srcdir}/${_pkgname}"
	git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	cd "${srcdir}/${_pkgname}"
	patch < ../0001-Add-transparency.patch
	ln -sf "${srcdir}/config.h" config.h
}

build() {
	cd "${srcdir}/${_pkgname}"
	make
}

package() {
	cd "${srcdir}/${_pkgname}"
	make PREFIX=/usr DESTDIR="${pkgdir}" TERMINFO="${pkgdir}/usr/share/terminfo" install
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
	install -Dm644 README FAQ -t "${pkgdir}/usr/share/doc/${pkgname}"
	install -Dm644 "${srcdir}/st-terminal.desktop" -t "${pkgdir}/usr/share/applications"
}
