# Maintainer: Tharre <tharre3@gmail.com>

pkgbase=arch-system
pkgname=('base-system' 'xps13')
pkgver=1
pkgrel=1
pkgdesc="Arch Linux system configuration"
arch=("any")
url=""
license=('custom')
source=('base-system.install' 'etc-pacman.conf' 'etc-locale.conf'
        'etc-locale.gen' 'etc-vconsole.conf' 'mirrorupgrade.hook'
        'sudoers-wheel' 'etc-nsswitch.conf' 'etc-sysctl.conf'

        'xps13-i915.conf')
sha256sums=('bb00c42f2802c12536c46700b948d41a43153d97a6986827b7c61203880e1ad1'
            'b3b540ac0dd68831d813e34dd8465cd00e8ba6a56fa6e5a162dc5ef469ec584c'
            '70b74b741f3b1b58eeb97d8d5b9e6148a35ff8dced47cf4a231c1d0420aff5eb'
            '61359bca48f246bc90d90204ad894bfc22665d789b130fc838e9926dc47ab3a3'
            '978271534da06356159eb24c0cee32b0024029d41df836ab76a964dfd7d36447'
            '79ba5fdf0c69686ea0f2770c9b49b58ca26ebcce15e346d5bd56a442e684896e'
            '62c1a085f7a9d1c5d57b40516fea5d7e8dfb3593d5c0a713cd571c8e5bae2584'
            '9c1648e1e9219efbe1e4098952f7595f1ebb0eca629d2c8957079186c56d1822'
            '67b6ebe8a8e52cd60266f418b7af77c2ef0b2b6596de494ec5c37824f355a3ae'
            '4e30b6dc062849a8217f419ca4752f062a8dafbbfb1f6f24ba894eb8dc78c736')

package_base-system() {
    depends=('gvim' 'zsh' 'tmux' 'htop' 'git' 'reflector' 'sudo' 'avahi'
             'nss-mdns' 'ttf-dejavu' 'chromium' 'ttf-liberation')

    install -Dm0644 etc-pacman.conf "$pkgdir"/etc/pacman.conf
    install -Dm0644 etc-locale.conf "$pkgdir"/etc/locale.conf
    install -Dm0644 etc-locale.gen "$pkgdir"/etc/locale.gen
    install -Dm0644 etc-vconsole.conf "$pkgdir"/etc/vconsole.conf
    install -Dm0644 mirrorupgrade.hook -t "$pkgdir"/etc/pacman.d/hooks
    install -dm0750 "$pkgdir"/etc/sudoers.d/
    install -Dm0644 sudoers-wheel "$pkgdir"/etc/sudoers.d/00_wheel
    install -Dm0644 etc-nsswitch.conf "$pkgdir"/etc/nsswitch.conf
    install -Dm0644 etc-sysctl.conf "$pkgdir"/etc/sysctl.d/99-sysctl.conf

    ln -s /usr/share/zoneinfo/Europe/Berlin "$pkgdir"/etc/localtime
}

package_xps13() {
    depends=('mesa' 'lib32-mesa' 'vulkan-intel' 'libva-intel-driver'
             'lib32-libva-intel-driver' 'libvdpau-va-gl')

    install -Dm0644 xps13-i915.conf "$pkgdir"/etc/modprobe.d/i915.conf
}

# TODO: docker, vagrant, devtools
