# Maintainer: Tharre <tharre3@gmail.com>

pkgname=albion-online-launcher-bin
pkgver=1.0.34.156
pkgrel=1
pkgdesc="The first true cross-platform Sandbox MMO -- launcher client"
url="https://albiononline.com/"
arch=('x86_64')
license=('custom')
depends=('libgl' 'qt5-webengine' 'xdelta3' 'libxinerama' 'ttf-font' 'sndio')
makedepends=('patchelf')
source=(${pkgname}-${pkgver}.zip::"https://live.albiononline.com/clients/albion-online-setup"
        "Albion-Online" "albion-online-launcher.desktop")
options=(!strip)
sha256sums=('fffe4bbfbcf181f0716b6fa34dc1bb28b885545553cce6535e79c829ec234388'
            '6e3967409add36965304f7c09567b274c1e7d1dbd090d20d904c4180976cd965'
            '33dd2dde06e27a18b04296c53998464080ccfd025bd0674acd7f08e8dc003660')

prepare() {
    cd data/launcher
    patchelf --remove-rpath Albion-Online
}

package() {
    cd data/launcher
    mkdir -p "${pkgdir}/usr/share/albion-online/launcher"
    cp -a Albion-Online version.txt *.qm "${pkgdir}/usr/share/albion-online/launcher"
    cd -

    install -Dm644 "${srcdir}/albion-online-launcher.desktop" -t "${pkgdir}/usr/share/applications"
    install -Dm644 data/AlbionOnline.xpm -t "${pkgdir}/usr/share/albion-online/"
    install -Dm644 data/eula.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
    install -Dm755 Albion-Online -t "${pkgdir}/usr/bin/"
}
