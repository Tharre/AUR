pkgname=spigot-custom
pkgver=1.8_R0.1
pkgrel=1
pkgdesc="High performance Minecraft server implementation"
arch=(any)
url="http://www.spigotmc.org"
license=("LGPL")
depends=(java-runtime-headless screen fontconfig)
makedepends=(java-environment git)
conflicts=(bukkit craftbukkit spigot-patcher spigot)
provides=(bukkit "craftbukkit=${pkgver%_*}")
install=spigot.install
source=("https://hub.spigotmc.org/jenkins/job/BuildTools/lastSuccessfulBuild/artifact/target/BuildTools.jar"
"spigot.service")
noextract=("BuildTools.jar")
md5sums=('6aa7ab8cd7f72841cb1d4084c9d5b0ce'
         '1c35e064da8ff6b62b804940309c651f')

build() {
	cd "$srcdir"
	java -jar BuildTools.jar
}

package() {
	install -Dm644 "$srcdir/Spigot/Spigot-Server/target/spigot-${pkgver//_/-}-SNAPSHOT.jar" "$pkgdir/srv/craftbukkit/spigot.jar"
	install -Dm644 "$srcdir/spigot.service" "$pkgdir/usr/lib/systemd/system/spigot.service"
}
